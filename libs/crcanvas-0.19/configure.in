m4_define([cr_major_version], [0])
m4_define([cr_minor_version], [19])
m4_define([cr_version], [cr_major_version.cr_minor_version])

AC_INIT([crcanvas], [cr_version],
        [http://sourceforge.net/tracker/?group_id=97070&atid=616883],
        [crcanvas])
AC_CONFIG_SRCDIR(crcanvas/cr-canvas.h)
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

CR_MAJOR_VERSION=cr_major_version
CR_MINOR_VERSION=cr_minor_version
CR_VERSION=cr_version
AC_SUBST(CR_MAJOR_VERSION)
AC_SUBST(CR_MINOR_VERSION)
AC_SUBST(CR_VERSION)


AC_PROG_CC
AM_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl cairo 1.2.4 is required but gtk 2.10 requires this
pkg_modules="gtk+-2.0 >= 2.10.0"
PKG_CHECK_MODULES(CR, [$pkg_modules],,
        [AC_MSG_RESULT(Could not find required dependencies $pkg_modules);
         exit 1;])
AC_SUBST(CR_CFLAGS)
AC_SUBST(CR_LIBS)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

GTK_DOC_CHECK([1.3])

#################
# Python wrapper

AC_ARG_ENABLE(python, [  --enable-python=[no/yes]  create a Python bindings (needs Python 2.2) [default=auto]],, enable_python=yes)

if test "x$enable_python" = "xyes" ; then

AM_PATH_PYTHON
if test "x$PYTHON" = "x" ; then
    enable_python=no
fi

dnl
dnl To cross compile for WIN32 on Linux, specify PYTHON_CFLAGS and PYTHON_LIBS
dnl explicitly.
dnl
if test "x${PYTHON_INCLUDES}" = "x"; then

        dnl
        dnl This header check will not work when cross compiling so 
        dnl the user needs to specify PYTHON_INCLUDES explicitly
        dnl
        AM_CHECK_PYTHON_HEADERS(,[AC_MSG_RESULT(could not find Python headers)]
                [enable_python=no])
fi

dnl check for pygtk
PKG_CHECK_MODULES(PYGTK, pygtk-2.0 >= 2.0 pycairo >= 1.2.0 , \
		AC_MSG_RESULT(yes), [ \
		AC_MSG_RESULT([no reqrs pygtk-2.0 >= 2.0 pycairo >= 1.2.0])
		enable_python=no])
AC_SUBST(PYGTK_CFLAGS)

AC_PATH_PROG(PYGTK_CODEGEN, pygtk-codegen-2.0, no)
if test "x$PYGTK_CODEGEN" = xno; then
    dnl AC_MSG_RESULT(could not find pygtk-codegen-2.0 script)
    enable_python=no
fi

AC_MSG_CHECKING(for pygtk defs)
PYGTK_DEFSDIR=`$PKG_CONFIG --variable=defsdir pygtk-2.0`
AC_SUBST(PYGTK_DEFSDIR)
AC_MSG_RESULT($PYGTK_DEFSDIR)

fi # enable_python

AM_CONDITIONAL(ENABLE_PYTHON, test "x$enable_python" = "xyes")

case "$host" in
        *-*-mingw*|*-*-cygwin*)
        platform_win32=yes
        ;;
        *)
        platform_win32=no
        ;;
esac

AM_CONDITIONAL(PLATFORM_WIN32, test "$platform_win32" = "yes")

AC_OUTPUT([
           Makefile
           crcanvas.pc
           crcanvas/Makefile
           crcanvas/cr-version.h
           cross-build/Makefile \
           unit-test/Makefile
           examples/Makefile
           doc/Makefile
           doc/ref/Makefile
           python/Makefile
           ])
echo ""
if test x"$enable_python" = "xno" ; then
    echo "crcanvas will NOT be compiled with Python wrappers."
    echo "pygtk-2.0 and pycairo are required."
else
    echo "crcanvas will be compiled with Python wrappers."
fi
